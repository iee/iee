grammar org.eclipse.iee.translator.jmole.math.Math with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate math "http://www.eclipse.org/iee/translator/jmole/math/Math"

Statement:
	functionDefinition=FunctionDefinition |
	variableAssignment=VariableAssignment |
	formula=Formula |
	matrixAssignment=MatrixAssignment |
	matrixFormula=MatrixFormula;

VariableAssignment:
	variable=MATH_NAME '=' value=Formula;

Formula:
	expression=(Addition);

	// addition/subtraction: left associative, priority 0
Addition returns Expression:
	Multiplication ('+' {Addition.left=current} right=Multiplication | '-' {Subtraction.left=current}
	right=Multiplication)*;

	// multiplication/division, left associative, priority 1
Multiplication returns Expression:
	UnaryExpression ('*' {Multiplication.left=current} right=UnaryExpression | '/' {Division.left=current}
	right=UnaryExpression | '%' {Modulo.left=current} right=UnaryExpression)*;

	// Unary operators: right associative, priority 2
UnaryExpression returns Expression:
	Exponent |
	'(-' {Invert} expression=UnaryExpression')' |
	'(' {Factorial} expression=UnaryExpression ')!';
	
	// exponentiation: right associative, priority 3
Exponent returns Expression:
	Primary ({Exponent.left=current} '^' right=Primary)?;

Primary returns Expression:
	{Variable} name=MATH_NAME |
	{Float} value=Float |
	{MatrixElement} element=MATH_NAME '[' row=INT ']' '[' column=INT ']' | 
	{Function} function=Function |
	'(' Addition ')';

Matrix:
	'{' (rows+=MatrixRow (',' rows+=MatrixRow)* ','?)? '}';
	
MatrixRow:
	'{' (elements+=Float (',' elements+=Float)* ','?)? '}';

MatrixAssignment:
	'[' variable=MATH_NAME ']' '=' value=MatrixFormula;

MatrixFormula:
	expression=MatrixAddition;

MatrixAddition returns MatrixExpression:
	MatrixMultiplication ('+' {MatrixAddition.left=current} right=MatrixMultiplication | '-' {MatrixSubtraction.left=current}
	right=MatrixMultiplication)*;
	
MatrixMultiplication returns MatrixExpression:
	PrimaryMatrix ('*' {MatrixMultiplication.left=current} rightMatrix=PrimaryMatrix |
				   '*' {MatrixMultiplication.left=current} rightScalar=Formula
	)*;
	
PrimaryMatrix returns MatrixExpression:
	{NewMatrix} matrix=Matrix |
	{TransposeMatrix} '[' name=MATH_NAME ']^T'|
	{MatrixVariable} '[' name=MATH_NAME ']' |
	'(' MatrixAddition ')';

Float:
	INT+ ('.' INT+ (('E' | 'e') ('-')? (INT)+)?)?;

Function returns Expression:
	name=MATH_NAME '(' (parameters+=Formula (',' parameters+=Formula)* ','?)? ')';

FunctionDefinition:
	function=Function '=' formula=Formula;
	
terminal MATH_NAME returns ecore::EString:
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;